export
    sequenceToList,

    all,
    any,
    forEach,
    map,
    reduce;

import Sequences from .Sequences;
import Lists from .Platform.Lists;

val sequenceToList = Lists.sequenceToList;

// TODO: use function composition

fun all(elements: List[Bool]) -> Bool {
    Sequences.all(Lists.listToSequence(elements))
}

fun any(elements: List[Bool]) -> Bool {
    Sequences.any(Lists.listToSequence(elements))
}

fun flatten[T](lists: List[List[T]]) -> List[T] {
    Lists.sequenceToList(
        Sequences.flatten(
            Sequences.lazyMap(fun (list: List[T]) => Lists.listToSequence(list), Lists.listToSequence(lists)),
        ),
    )
}

fun forEach[T, !E](func: Fun (T) !E -> Unit, elements: List[T]) !E -> Unit {
    Sequences.forEach!(func, Lists.listToSequence(elements))
}

fun map[T, R, !E](func: Fun (T) !E -> R, elements: List[T]) !E -> List[R] {
    Lists.sequenceToList!(
        Sequences.lazyMap(func, Lists.listToSequence(elements)),
    )
}

fun reduce[T, R, !E](func: Fun (R, T) !E -> R, initial: R, elements: List[T]) !E -> R {
    Sequences.reduce!(
        func,
        initial,
        Lists.listToSequence(elements),
    )
}
