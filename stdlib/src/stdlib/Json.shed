import stdlib.Lists;
import stdlib.Strings;


union JsonValue = JsonObject | JsonArray | JsonNumber | JsonString | JsonBoolean | JsonNull;

shape JsonObject memberOf JsonValue {
    properties: List[JsonProperty],
}

fun object(properties: List[JsonProperty]) -> JsonValue {
    JsonObject(properties = properties)
}

shape JsonProperty {
    key: String,
    value: JsonValue,
}

fun property(key: String, value: JsonValue) -> JsonProperty {
    JsonProperty(key = key, value = value)
}

shape JsonArray memberOf JsonValue {
    elements: List[JsonValue],
}

fun array(elements: List[JsonValue]) -> JsonValue {
    JsonArray(elements = elements)
}

shape JsonNumber memberOf JsonValue {
    value: String,
}

fun number(value: String) -> JsonValue {
    JsonNumber(value = value)
}

shape JsonString memberOf JsonValue {
    value: String,
}

fun string(value: String) -> JsonValue {
    JsonString(value = value)
}

shape JsonBoolean memberOf JsonValue {
    value: Bool,
}

fun boolean(value: Bool) -> JsonValue {
    JsonBoolean(value = value)
}

shape JsonNull memberOf JsonValue {
}

val null = JsonNull();

fun valueToText(value: JsonValue) -> String {
    when (value) {
        is JsonObject {
            val properties = Strings.join(",", Lists.map(propertyToText, value.properties));
            "{" + properties + "}"
        }
        is JsonArray {
            val elements = Strings.join(",", Lists.map(valueToText, value.elements));
            "[" + elements +  "]"
        }
        is JsonNumber {
            value.value
        }
        is JsonString {
            jsonStringToText(value.value)
        }
        is JsonBoolean {
            if (value.value) {
                "true"
            } else {
                "false"
            }
        }
        is JsonNull {
            "null"
        }
    }
}

fun propertyToText(property: JsonProperty) -> String {
    jsonStringToText(property.key) + ":" + valueToText(property.value)
}

fun jsonStringToText(value: String) -> String {
    // TODO: handle special characters
    val characters = value
        |> Strings.replace ~ ("\\", "\\\\")
        |> Strings.replace ~ ("\"", "\\\"");
    "\"" + characters + "\""
}
