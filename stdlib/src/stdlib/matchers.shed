import .options;

fun anything() -> Matcher[Any] {
    Matcher(
        description = "anything",
        match = fun (x: Any) => success,
    )
}

fun isInteger(value: Int) -> Matcher[Int] {
    describingMatcher(
        value,
        fun (integer: Int) => intToString(integer),
        fun (actual: Int) => actual == value,
    )
}

fun isString(value: String) -> Matcher[String] {
    describingMatcher(
        value,
        fun (string: String) => string,
        fun (actual: String) => actual == value,
    )
}

fun isNone() -> Matcher[options.Option[Any]] {
    Matcher(
        description = "none",
        match = fun (actual: options.Option[Any]) -> Result {
            if (actual is options.None) {
                success
            } else {
                failure("was some()")
            }
        }
    )
}

fun isSome[T](matcher: Matcher[T]) -> Matcher[options.Option[T]] {
    Matcher(
        description = "option with value " + matcher.description,
        match = fun (actual: options.Option[T]) -> Result {
            if (actual is options.Some[T]) {
                matcher.match(actual.value)
            } else {
                failure("was none")
            }
        }
    )
}

fun describingMatcher[T](
    value: T,
    toString: (T) -> String,
    match: (T) -> Bool
) -> Matcher[T] {
    Matcher(
        description = toString(value),
        match = fun (actual: T) -> Result {
            if (match(actual)) {
                success
            } else {
                failure("was " + toString(actual))
            }
        },
    )
}

shape Matcher[-T] {
    description: String,
    match: (T) -> Result,
}

union Result = Success | Failure;
shape Success memberOf Result { }
shape Failure memberOf Result {
    message: String,
}
val success = Success();
fun failure(message: String) -> Failure {
    Failure(message=message)
}
